#!/bin/zsh --source

NOCOL=$'%{\e[0m%}'

#tlcr-check()
#{
#    local ret="$?"
#    if [[ $ret = 0 ]] ; then
#	echo -n "${_GREEN}tlcr: $ret${NOCOL}"
#    else
#	echo -n "${_RED}tlcr: $ret${NOCOL}"
#    fi
#}

# Git

if [[ "$(uname -o)" = 'Msys' ]]; then # on windows it lags significantly
    ENABLE_GIT_PROMPT=false
else
    ENABLE_GIT_PROMPT=true
fi

get_git_status () {
    unset __CURRENT_GIT_BRANCH
    unset __CURRENT_GIT_BRANCH_STATUS
    unset __CURRENT_GIT_BRANCH_IS_DIRTY
    [[ "$PWD" = "$HOME" ]] && return
    local dir="$(c git rev-parse --git-dir 2>/dev/null)"
    [[ "${dir:h}" = "$HOME" ]] && return
    local st="$(c git status 2>/dev/null)"
    if [[ -n "$st" ]]
    then
        local -a arr
        arr=(${(f)st})
        if [[ $arr[1] =~ 'Not currently on any branch.' ]]
        then
            __CURRENT_GIT_BRANCH='no-branch'
        else
            __CURRENT_GIT_BRANCH="${arr[1][(w)4]}"
        fi
        if [[ $arr[2] =~ 'Your branch is' ]]
        then
            if [[ $arr[2] =~ 'ahead' ]]
            then
                __CURRENT_GIT_BRANCH_STATUS=ahead
            elif [[ $arr[2] =~ 'diverged' ]]
            then
                __CURRENT_GIT_BRANCH_STATUS=diverged
            else
                __CURRENT_GIT_BRANCH_STATUS=behind
            fi
        fi
        [[ ! $st =~ "nothing to commit" ]] && __CURRENT_GIT_BRANCH_IS_DIRTY=1
    fi
    return 0
}

get_prompt_git ()
{
    if [[ -n $__CURRENT_GIT_BRANCH ]]
    then
        local s=$__CURRENT_GIT_BRANCH
        case "$__CURRENT_GIT_BRANCH_STATUS" in
            (ahead) s+="+"  ;;
            (diverged) s+="="  ;;
            (behind) s+="-"  ;;
        esac
        [[ $__CURRENT_GIT_BRANCH_IS_DIRTY = '1' ]] && s+="*"
        echo "${_YELLOW}$s${NOCOL}"
    fi
}

git_branch_chpwd ()
{
    get_git_status
    return 0
}

git_branch_precmd ()
{
    [[ "$(fc -l -1)" = *git* ]] && get_git_status
    return 0
}

# Fish-style 的截短路径
prompt_pwd ()
{
    # tlcr: 128 2019,xx,xx(x) xx,xx,xx `user`@`hostname` `dir` git-branch
    local usedwidth=$[ 9 + 14 + 9 + ${#USER} + 4 + ${#HOST} + ${#__CURRENT_GIT_BRANCH} + 2 ]
    local ppwd="${^${(%):-%~}}" restwidth=$[$COLUMNS - $usedwidth]
    if [ ${#ppwd} -gt $restwidth ] ; then
        local bfpwd="$(echo "$ppwd" | \egrep -o '^~\w+/')"
        ppwd="${ppwd#$bfpwd}"
        ppwd="$bfpwd$(echo "$ppwd" | sed -e 's!([^/])[^/]+?/!\1/!g')"
    fi
    #_prompt_pwd="$_prompt_color$bg_MAGENTA$ppwd$NOCOL"
    ppwd="${ppwd%/}/"
    _prompt_pwd="${ppwd//\//$_WHITE/$_CYAN}" # `/'用白色粗体显示
}

setopt promptsubst
_DATE_NOW="${_BLUE}%D{%Y,%-m,%-d (%u)} ${_MAGENTA}%D{%H,%M,%S}"
if [[ $UID -eq 0 ]]; then
    _USER="${_RED}$USER${NOCOL}"
else
    _USER="${_GREEN}$USER${NOCOL}"
fi

_HOSTNAME="${_GREEN}%m${NOCOL}"
_TLCR="%(?!${_GREEN}tlcr: \$?${NOCOL}!${_RED}tlcr: \$?${NOCOL})"
_JOBS="%(1j!%U${_YELLOW}jobs: %j${NOCOL}%u !)" #!${_GREEN}jobs: %j${NOCOL})"

__colors=(red green yellow blue magenta cyan)
__COLORS=(RED GREEN YELLOW BLUE MAGENTA CYAN)
__num=$[$RANDOM % ${#__colors}]
__num=$[(__num - 1) % ${#__colors} +2]
__color=${__colors[$__num]}
__COLOR=${__COLORS[$__num]}

# Ctrl+@ 设置标记，标记和光标点之间为 region
zle_highlight=(
    region:bg=magenta
    special:bold,fg=magenta # 特殊
    isearch:underline # ???
    default:bold,fg=$__color # 输入
)
# 提示符和输入的命令同一颜色
PROMPT="${NOCOL}$_TLCR $_JOBS$_DATE_NOW ${_USER}@$_HOSTNAME $_CYAN\$_prompt_pwd$NOCOL \$(get_prompt_git)
\${_$__COLOR}]$NOCOL "
PROMPT2="${RED}%_\${_$__COLOR}>$NOCOL "
unset __colors __COLORS __num __color __COLOR

prompt_pwd
if [[ $ENABLE_GIT_PROMPT == true ]]; then
    git_branch_chpwd
fi

SPROMPT="${_YELLOW}修正${NOCOL}:${_RED}%R${NOCOL}->${_GREEN}%r${NOCOL}:[${_CYAN}N${NOCOL}o/${_CYAN}y${NOCOL}es/${_CYAN}e${NOCOL}dit/${_CYAN}a${NOCOL}bort] ${_YELLOW}=>${NOCOL} "
export SUDO_PROMPT=$'sudo: Password for \e[1;33m%p\e[0m:'

preexec_functions=()
if [[ $ENABLE_GIT_PROMPT == true ]]; then
    precmd_functions=(git_branch_precmd prompt_pwd)
    chpwd_functions=(git_branch_chpwd prompt_pwd)
else
    precmd_functions=(prompt_pwd)
    chpwd_functions=(prompt_pwd)
fi

screentitle ()
{
    print -Pn "\ek$*\e\\"
}

typeset -A _prefixes
_prefixes=(sudo '#' e '' c '' ssh '@' man '?' perldoc 'P?' gfw '^' nice '-')
_commandPrefixes=(sudo e c gfw nice)
#_replaces=('#' '@' '?' 'P?')

screenTitlePreexec()
{
    local cmd="$1" prefix continue
    while : ; do
        continue=0
        for i in ${(k)_prefixes} ; do
            #echo $cmd, $i,$continue

            if [[ "$cmd" = "$i "* ]] ; then
                #echo match "$i"
                cmd="${cmd#$i }"
            #cmd="${cmd%% *}"
            #cmd="${cmd##*/}"
                prefix="$prefix$_prefixes[$i]"
            #cmd="$_prefixes[$i]""$cmd"
            #break
                if inList "$i" "${_commandPrefixes[@]}" ; then
                    continue=1
                    #echo "command:$i"
                else
                    break
                fi
            fi
        done
        if [[ "$continue" = 0 ]] ; then
            #echo break
            break
        fi
    done

    if [[ -z "$cmd" ]] ; then
        cmd="$1"
    fi
    cmd="${cmd%% *}"
    cmd="${cmd##*/}"
#    cmd="${cmd%% *}"
#    cmd="${cmd%%-*}"
#    cmd="${cmd##*/}"
    screentitle "$prefix$cmd"
}

screenTitlePrecmd()
{
    local dir="${^${(%):-%~}}"
    dir="${dir##*/}"
    screentitle ":$dir"
}

if [[ "$TERM" = screen* ]] ; then
    preexec_functions+=(screenTitlePreexec)
    precmd_functions+=(screenTitlePrecmd)
fi
